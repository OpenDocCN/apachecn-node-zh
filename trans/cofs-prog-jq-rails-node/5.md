# 第五章。咖啡脚本和节点

Ryan Dahl 在 2009 年创建了 Node.js。他的目标是创建一个系统，使用 JavaScript 编写高性能的网络服务器应用。当时，JavaScript 主要在浏览器内部运行，因此服务器端框架需要某种方式来运行没有它的 JavaScript。Node 使用谷歌的 V8 JavaScript 引擎，最初是为 Chrome 浏览器编写的，但由于它是一个独立的软件，所以它可以在任何地方运行 JavaScript 代码。Node.js 允许您编写可以在服务器上执行的 JavaScript 代码。它可以充分利用您的操作系统、数据库和其他外部网络资源。

让我们来谈谈 Node.js 的一些特性

# 节点是事件驱动的

Node.js 框架只允许非阻塞、异步的输入/输出。这意味着任何访问外部资源(如操作系统、数据库或网络资源)的输入/输出操作都必须异步进行。这是通过使用事件或回调来实现的，一旦操作成功或失败，就会触发这些事件或回调。

这样做的好处是，您的应用变得更加可扩展，因为请求不必等待缓慢的输入/输出操作完成，而是可以处理更多的传入请求。

其他语言中也确实存在类似的框架，比如 Python 中的 **Twisted** 和**Tornado**T5，Ruby 中的**event machine**T8。这些框架的一个大问题是，它们使用的所有输入/输出库也必须是非阻塞的。通常，人们可能会意外地使用阻止输入/输出操作的代码。

Node.js 是基于事件驱动的理念从头开始构建的，只允许无阻塞的 I/O，从而避免了这个问题。

# 节点快速且可扩展

Node.js 使用的 V8 JavaScript 引擎针对性能进行了高度优化，从而使 Node.js 应用运行速度非常快。Node 是非阻塞的这一事实将确保您的应用能够在不使用大量系统资源的情况下处理许多并发的客户端请求。

# 节点不是 Rails

虽然 Node 和 Rails 经常被用来构建类似类型的应用，但它们实际上是非常不同的。Rails 致力于成为构建 web 应用的全栈解决方案，而 Node.js 更像是一个低级系统，用于编写任何类型的快速且可扩展的网络应用。除了使用基于事件的体系结构这一事实之外，它根本没有对应用应该如何构造做出很多假设。

正因为如此，Node 开发人员经常从各种框架和模块中进行选择，这些框架和模块都是在 Node 之上构建的，用于编写 web 应用，例如 Express 或 Flatiron。

# 节点和咖啡脚本

正如我们之前看到的，咖啡脚本作为一个 npm 模块提供。因此，用咖啡脚本编写 Node.js 应用再容易不过了。事实上，我们前面讨论的的`coffee`命令将默认使用 Node 运行`.coffee`脚本。要让节点安装咖啡脚本，请参见[第 2 章](2.html "Chapter 2. Running CoffeeScript")、*运行咖啡脚本*。

# 《你好世界》中的节点

让我们使用咖啡脚本编写最简单的节点应用。创建一个名为`hello.coffee`的文件，并在其中输入以下代码:

```js
http = require('http')

server = http.createServer (req, res) ->
  res.writeHead 200
  res.end 'Hello World'

server.listen 8080
```

这使用了 Node.js 的`http`模块，它提供了构建 HTTP 服务器的功能。`require('http')`函数将返回一个`http`模块的实例，该实例导出一个`createServer`函数。该函数采用`requestListener`参数，这是一个响应客户请求的函数。在这种情况下，我们使用 HTTP 状态代码`200`进行响应，并以`Hello World`作为请求主体来结束响应。最后，我们在返回的服务器上调用`listen`方法来启动它。当调用此方法时，服务器将监听并处理请求，直到我们停止它。

我们可以用 coffee 命令运行这个文件，如下命令所示:

```js
coffee hello.coffee
```

我们可以通过浏览`http://localhost:8080/`来测试我们的服务器。我们应该看到一个简单的页面，只有文本为**你好世界**。

# 快车

可以看到，开箱节点非常低级，裸骨。构建 web 应用基本上意味着编写一个原始的 HTTP 服务器。幸运的是，在过去的几年里，已经开发了许多库来帮助在 Node 上编写 web 应用，并抽象出许多低级细节。

可以说，其中最受欢迎的是**快车**([http://expressjs.com/](http://expressjs.com/))。与 Rails 类似，它有很多很好的特性，使得执行常见的 web 应用任务变得更加容易，例如路由、呈现视图和托管静态资源。

在本章中，我们将使用咖啡脚本在 Express 中编写一个网络应用。

# 网页 Socket

因为我想展示一下 Node 的一些可伸缩性特性和它通常用于的应用类型，我们将使用另一种有趣的现代网络技术，称为**网络套接字**。

网络套接字协议是允许通过标准 HTTP 端口 **80** 进行原始、双向和全双工(双向同时)TCP 连接的标准。这允许客户端和服务器建立长时间运行的 TCP 连接，服务器可以使用该连接执行推送操作，这在传统的 HTTP 中是不可能的。它通常用于客户端和服务器之间需要大量低延迟交互的应用。

# 玉

Jade 是一种轻量级的标记模板语言，它允许你用一种非常类似于 CoffeeScript 的语法来编写优雅而简短的 HTML。它使用了相当多的功能，比如语法空白来减少编写 HTML 文档所需的击键次数。它通常在您运行 Express 时默认安装，我们将在本书中使用它。

# 我们的应用

在本章中，我们将构建一个协作待办事项应用。这意味着您将能够与他人实时共享您的待办事项列表。一个或多个用户将能够同时添加、完成或删除待办事项。对待办事项列表的更改将自动传播给所有用户。这是 Node 最适合的应用类型。

我们的 Node.js 代码将由两个不同的部分组成，一个是提供静态 HTML、CSS 和 JavaScript 的普通 web 应用，另一个是处理所有待办事项客户端实时更新的 WebSocket 服务器。与此同时，我们将有一个 jQuery 驱动的客户端，看起来非常类似于我们在[第 3 章](3.html "Chapter 3. CoffeeScript and jQuery")、*咖啡脚本和 jQuery* 中的应用。

我们将使用现有待办事项应用中的一些资源(样式表和图像)。我们还将重新使用来自[第 3 章](3.html "Chapter 3. CoffeeScript and jQuery")、*咖啡脚本和*的客户端 jQuery 代码，并对其进行调整以适应我们的应用。如果您没有在前面的章节中继续学习，您应该能够根据需要从本章的代码中复制资产。

# 我们开始吧

要开始，我们将执行以下步骤:

1.  为我们的应用创建文件夹。
2.  使用`package.json`文件指定我们的应用依赖关系。
3.  安装我们的依赖项。
4.  创建`app.coffee`文件。
5.  第一次运行我们的应用。

## package.json

创建一个名为`todo`的新文件夹。在这个文件夹中，我们将创建一个名为`package.json`的文件。将以下代码添加到该文件中:

```js
{
  "name": "todo",
  "version": "0.0.1",
  "private": true,
  "scripts": {
    "start": "node app"
  },
  "dependencies": {
    "express": "3.0.0beta6",
    "jade": "*",
    "socket.io": "*",
    "coffee-script": "*",
    "connect-assets": "*"
  }
}
```

这是一个简单的 JSON 文件，用作应用清单，用于告诉 npm 您在应用中依赖哪些依赖项。这里，我们使用 Express 作为我们的网络框架，Jade 作为我们的模板语言。既然我们要使用网络套接字，我们就引入`socket.io`。我们还可以通过将 CoffeeScript 添加到我们的文件中来确保它已经安装。最后，我们将使用`connect-assets`，一个管理客户端资产的模块，其方式与 Rails 资产管道非常相似。

在处理 Node.js 框架时，您会注意到应用经常以这种方式由 npm 模块编织而成。寻找 npm 模块的好地方是 Node 工具箱站点([nodetoolbox.com](http://nodetoolbox.com))。

## 安装我们的模块

要在我们的`package.json`文件中安装依赖项，请导航到命令行工具上的项目文件夹，并运行以下命令:

```js
npm install
```

如果一切顺利，那么我们现在应该已经安装了所有的项目依赖项。要验证这一点或只是查看 npm 做了什么，您可以运行以下命令:

```js
npm ls
```

这将以树状格式输出已安装模块及其依赖项的列表。

## 创建我们的应用

我们只需要运行我们的应用来创建一个主入口点文件，该文件用于连接我们的快递应用并指定我们的路线。在根文件夹中，创建一个名为`app.coffee`的文件，并向其中添加以下代码:

```js
express = require 'express'
app = express()

app.get '/', (req, res) ->
  res.send('Hello Express')

app.listen(3000)
console.log('Listening on port 3000')
```

这看起来与我们的“Hello World”示例非常相似。

首先，我们使用`require`功能加载 Express 模块。节点模块简单；每个模块对应一个文件。每个模块都可以声明代码，当需要时会导出代码。当你调用`require`时，模块名称不是原生模块或文件路径，Node 会自动在`node_modules`文件夹中查找文件。这当然是 npm 安装模块的地方。

在下一行，我们通过调用`express`函数并将其分配给`app`变量来创建我们的 Express 应用。

然后，我们使用`get`方法为我们的应用创建一个索引路径。我们将路径指定为`'/'`，然后传入一个匿名函数来处理请求。它需要两个参数，`req`和`res`参数。现在，我们只需写`Hello Express`到回复并返回。

然后，我们使用`listen`方法启动我们的应用，并告诉它在端口`3000`上运行。最后，我们写入标准输出，这样我们就知道应用已经启动。

正如你所看到的，快递的魔力在于以声明的方式设置路线。通过快速，您可以通过指定 HTTP 方法、URL 路径和处理请求的函数来轻松创建路由。

## 运行我们的应用

让我们运行我们的应用看看是否一切正常。在我们的应用文件夹中，在命令行工具上键入以下内容:

```js
coffee app.coffee
```

您应该看到输出为**在端口 3000** 上监听。

将浏览器指向`http://localhost:3000/`。你应该看看文字**你好快递**。

要在命令行工具上停止节点进程，只需使用 *Ctrl* + *C* 。

# 创建视图

与 Rails 等其他 web 框架类似，Express 也有视图的概念，它允许您使用单独的文件将 UI 与应用分开。通常，这些是使用模板语言编写的，比如 Jade。让我们为根操作创建一个视图。

为此，我们需要:

1.  创建一个`views`文件夹，并添加一个 Jade 视图文件。
2.  将我们的 Express 应用配置为知道视图将存储在哪个文件夹中，以及我们正在使用哪个模板库。
3.  更改我们的索引路径以呈现我们的视图。

让我们在项目根目录下创建一个名为`views`的新文件夹。在这个文件夹中，我们创建了一个名为`index.jade`的新文件。应该是这样的:

```js
doctype 5
html
  head
    title Our Jade view
  body
    p= message
```

正如您所看到的，Jade 为普通的 HTML 提供了非常简洁的语法。尖括号中没有封闭标记。类似于 CoffeeScript，它还使用缩进来分隔块，这样您就不必输入结束标记。第`p= message`行创建一个`<p>`标签，其内容将被评估为`message`字段的值，该值应该被传递到我们的视图选项中。

在我们的`app.coffee`文件中，我们将添加以下代码:

```js
express = require 'express'
path = require 'path'
app = express()

app.set 'views', path.join __dirname, 'views'
app.set 'view engine', 'jade'

app.get '/', (req, res) ->
 res.render 'index', message: "Now we're cooking with gas!"

app.listen(3000)
console.log('Listening on port 3000')
```

这里，我们使用`set`功能设置`views`文件夹，并分配`'views'`键。我们使用文件顶部的`path`模块来创建我们当前的文件夹名称并将其连接到`views`子文件夹。`__dirname`是指当前工作文件夹的全局变量。我们还将视图引擎设置为`'jade'`。

接下来，我们更改我们的`get '/'`路线来渲染索引模板，并传递一个包含消息的选项散列。这是在我们的视图中呈现的值。

一旦我们再次运行我们的应用并刷新页面，我们现在应该看到我们的页面已经用新的文本进行了更新。

# 节点-主管

到目前为止，您可能想知道，每次我们对代码进行更改时，您是否需要重新启动我们的 Node 应用。理想情况下，在开发中，我们希望我们的代码在每次进行更改时自动重新加载，类似于它在 Rails 中的工作方式。

幸运的是，这里 是一个整洁的开源库，我们可以使用它来实现这一点:**节点主管**([https://github.com/isaacs/node-supervisor](https://github.com/isaacs/node-supervisor))。我们像安装任何其他 npm 模块一样安装它，我们只需确保通过`-g`标志来全局安装它，如下命令所示:

```js
npm install supervisor -g
```

在终端中，您现在应该能够使用以下命令运行管理器:

```js
supervisor app.coffee
```

请在单独的窗口中运行此过程。要查看这是否有效，让我们编辑发送到我们视图的消息；编辑后的消息在以下代码片段中突出显示:

```js
app.get '/', (req, res) ->
 res.render 'index', message: "Now we're cooking with supervisor!"

```

如果我们现在刷新我们的页面，我们将看到它已经被更新。从这里开始，我们可以确保管理器保持运行，并且我们不需要重新启动我们的节点进程来进行更改。

# 待办事项列表视图

现在让我们扩展我们的视图，看起来像我们真正的待办事项应用。编辑`index.jade`文件，如下所示:

```js
doctype 5
html
  head
 title Collaborative Todo
  body
 section#todoapp
 header#header
 h1 todos
 input#new-todo(placeholder="What needs to be done?", autofocus=true)
 section#main
 ul#todo-list
 footer#footer
 button#clear-completed Clear completed

```

这里有一些我们以前没有见过的新的 Jade 语法。标签号由`#`符号表示，因此`header#header`成为`<header id="header">`。标签属性在括号内指定，如:`tag(name="value")`。

由于我们不再使用模板中的`message`变量，我们将从`app.coffee`文件中的`render`调用中删除它，如以下代码片段所示:

```js
app.get '/', (req, res) ->
 res.render 'index'

```

我们的页面现在将被更新，但看起来不会太好。我们将使用与上一个项目中相同的样式表来设计页面样式。

### 类型

**不按预期工作？**

请记住关注主管流程的输出，查看您的咖啡脚本或 Jade 模板中是否有任何语法错误，尤其是如果您没有看到预期的输出。

在使用样式表之前，我们需要设置 Express 为我们提供静态文件。将`app.coffee`文件修改如下:

```js
express = require 'express'
path = require 'path'

app = express()

app.set 'views', path.join __dirname, 'views'
app.set 'view engine', 'jade'
app.use(express.static(path.join __dirname, 'public'))

```

那么在前面的代码片段中发生了什么呢？我们增加了对单行服务静态文件的支持，但是这是如何工作的呢？答案在于 Node 如何使用中间件。

## 中间件

Express 框架建立在名为**连接**([http://www.senchalabs.org/connect/](http://www.senchalabs.org/connect/))的底层框架之上。Connect 的基本思想是为 web 请求提供中间件。

中间件可以链接在一起产生一个网络应用堆栈。每一个中间件都只关心通过修改请求的输出响应或控制流来提供一小部分功能。

在我们的例子中，我们告诉我们的应用使用由`express.static`函数创建的中间件。该函数将为提供的文件路径创建一个静态文件服务器。

## 我们的样式表

创建名为`public`的文件夹，子文件夹名为`css`。将样式表另存为文件夹中的`todo.css`。我们仍然需要在`index`视图中包含样式表。将以下代码片段中突出显示的行添加到`views`文件夹中的`index.jade`文件中:

```js
doctype 5
html
  head
  title  Collaborative Todo
 link(rel="stylesheet", href="css/todo.css")
  body
```

一旦我们链接到样式表，我们应该能够刷新视图。现在看起来应该好多了。

# 客户端

为了使成为我们的待办应用，我们将复制我们在[第 3 章](3.html "Chapter 3. CoffeeScript and jQuery")、*咖啡脚本和*中创建的客户端 jQuery 代码。我们将把它放在一个名为`todo.coffee`的文件中。

我们的下一个决定是，我们把这个文件放在哪里？我们将如何在应用中编译和使用它的输出？

我们可以像在[第 3 章](3.html "Chapter 3. CoffeeScript and jQuery")、 *CoffeeScript 和 jQuery* 中构建我们的应用时一样做同样的事情，也就是说，创建一个包含客户端 offeeScript 代码的`src`文件夹，然后使用带有`--watch`标志的`coffee`命令编译它。输出的 JavaScript 然后可以进入我们的`public`文件夹，在那里我们可以像往常一样包含它。但这意味着我们将有两个独立的后台任务在运行，一个是主管任务，用于运行我们的服务器，另一个用于编译我们的客户端代码。

幸运的是，还有一个更好的方法。您可能还记得，我们在`package.json`文件中引用了`connect-assets`模块。它为我们提供了一个资产管道，与您在 Rails 中获得的非常相似。它将透明地处理编译和依赖管理。

我们需要在我们的`app.coffee`文件中使用中间件，如下面的代码片段所示:

```js
app.set 'views', path.join __dirname, 'views'
app.set 'view engine', 'jade'
app.use(express.static(path.join __dirname, 'public'))
app.use require('connect-assets')()

```

默认情况下，`connect-assets`模块将使用`assets`文件夹来管理和服务来自的资产。让我们在根文件夹中创建一个名为`assets/js`的文件夹。我们将在这个名为`todo.coffee`的文件夹中创建一个新文件，包含以下代码:

```js
Storage::setObj = (key, obj) ->
  localStorage.setItem key, JSON.stringify(obj)

Storage::getObj = (key) ->
  JSON.parse this.getItem(key)

class TodoApp

  constructor: ->
    @cacheElements()
    @bindEvents()
    @displayItems()

  cacheElements: ->
    @$input = $('#new-todo')
    @$todoList = $('#todo-list')
    @$clearCompleted = $('#clear-completed')

  bindEvents: ->
    @$input.on 'keyup', (e) => @create e
    @$todoList.on 'click', '.destroy', (e) => @destroy e.target
    @$todoList.on 'change', '.toggle', (e) => @toggle e.target
    @$clearCompleted.on 'click', (e) => @clearCompleted()

  create: (e) ->
    val = $.trim @$input.val()
    return unless e.which == 13 and val

    randomId = Math.floor Math.random()*999999

    localStorage.setObj randomId,{
      id: randomId
      title: val
        completed: false
    }
    @$input.val ''
    @displayItems()

  displayItems: ->
    @clearItems()
    @addItem(localStorage.getObj(id)) for id in Object.keys(localStorage)

  clearItems: ->
    @$todoList.empty()

  addItem: (item) ->
    html = """
      <li #{if item.completed then 'class="completed"' else ''} data-id="#{item.id}">
        <div class="view">
          <input class="toggle" type="checkbox" #{if item.completed then 'checked' else ''}>
          <label>#{item.title}</label>
          <button class="destroy"></button>
        </div>
     </li>
    """
    @$todoList.append html

  destroy: (elem) ->
    id = ($(elem).closest 'li').data('id')
    localStorage.removeItem id
    @displayItems()

  toggle: (elem) ->
    id = $(elem).closest('li').data('id')
    item = localStorage.getObj(id)
    item.completed = !item.completed
    localStorage.setObj(id, item)

  clearCompleted: ->
    (localStorage.removeItem id for id in Object.keys(localStorage) \
      when (localStorage.getObj id).completed)
    @displayItems()

$ ->
  app = new TodoApp()
```

如果你在[第三章](3.html "Chapter 3. CoffeeScript and jQuery")、*咖啡脚本和*中跟随，那么这个代码应该是熟悉的。这是我们完整的客户端应用，显示待办事项，并在`localStorage`中创建、更新和销毁项目。

为了在我们的 HTML 中使用这个文件，我们仍然需要包含一个`script`标签。由于我们使用的是 jQuery，我们还需要在 HTML 中包含这个库。

在`index.jade`文件的底部添加以下代码:

```js
script(src="//ajax.googleapis.com/ajax/libs/jquery/1.7.2/jquery.min.js")
!= js('todo')
```

如您所见，我们包含了一个使用谷歌 CDN 的 jQuery 链接。然后我们使用`connect-assets`提供的`js`助手函数来创建一个指向我们编译的`todo.js`文件的`script`标签(T4 模块将透明地编译我们的咖啡脚本)。`!=`符号是 Jade 运行 JavaScript 函数及其结果的语法。

如果一切顺利，我们应该能够刷新页面，并为我们的应用提供一个工作的客户端页面。尝试添加新项目、将项目标记为完成、删除项目和清除完成的项目。

# 添加协作

现在我们准备将协作添加到我们的待办事项列表应用中。我们需要创建一个页面，多个用户可以连接到同一个待办事项列表，并可以同时编辑，实时看到结果。

我们愿意支持命名列表的想法，你可以和其他人一起加入来合作。

在深入研究功能之前，让我们稍微调整一下用户界面，以支持所有这些功能。

# 创建协作界面

首先，我们将添加一个输入字段来指定一个列表名称，一个按钮来加入指定的列表。

对我们的`index.jade`文件进行以下更改(在代码片段中突出显示)，这将添加一个`input`元素和一个`button`元素来指定我们的列表名称并加入它:

```js
      footer#footer
 | Join list:
 input#join-list-name
 button#join Join
        button#clear-completed Clear completed
  script(src="//ajax.googleapis.com/ajax/libs/jquery/1.7.2/jquery.min.js")
  != js('todo')
```

我们的页面现在应该看起来像下面截图中显示的页面:

![Creating the collaboration UI](img/9588_05_01.jpg)

# 客户端上的 WebSocket

现在让我们添加一个事件处理程序，当用户点击**加入**按钮时连接到房间。

在我们的`todo.coffee`文件中，我们将向我们的`cacheElements`和`bindEvents`函数添加以下代码:

```js
cacheElements: ->
    @$input = $('#new-todo')
    @$todoList = $('#todo-list')
    @$clearCompleted = $('#clear-completed')
 @$joinListName = $("#join-list-name")
 @$join = $('#join')

  bindEvents: ->
    @$input.on 'keyup', (e) => @create e
    @$todoList.on 'click', '.destroy', (e) => @destroy e.target
    @$todoList.on  'change', '.toggle', (e) => @toggle e.target
    @$clearCompleted.on 'click', (e) => @clearCompleted()
 @$join.on 'click', (e) => @joinList()

```

我们抓取`join-list-name`输入和`join`按钮元素，并将它们存储在两个实例变量中。然后我们在`@$join`按钮上设置`click`处理程序，调用一个名为`joinList` 的新功能。现在让我们继续定义这个函数。在`bindEvents`函数定义后，将其添加到类的末尾:

```js
clearCompleted: ->
    (localStorage.removeItem id for id in Object.keys(localStorage) \
      when (localStorage.getObj id).completed)
    @displayItems()

 joinList: ->
 @socket = io.connect('http://localhost:3000')

 @socket.on 'connect', =>
@socket.emit 'joinList', @$joinListName.val()

```

这是我们开始使用套接字的地方。IO 库分为两部分:客户端库，用于打开 WebSocket 连接、发出请求和接收响应，以及服务器端节点模块，用于处理请求。

在前面的代码中，`joinList`函数使用`io.connect`函数打开一个新的套接字，并传入 URL。然后，它使用`on`函数传递一个处理函数，该函数将在网络套接字连接完成后运行。

成功的连接处理函数将依次使用`socket.emit`函数，它允许我们使用`joinList`作为标识符向服务器发送自定义消息。我们把`@joinListName` 的价值输入作为它的价值。

在我们开始实现服务器端代码之前，我们仍然需要包含一个`script`标签来使用`socket.io`客户端库。在`index.jade`文件底部添加以下高亮显示的 `script`标签:

```js
script(src="//ajax.googleapis.com/ajax/libs/jquery/1.7.2/jquery.min.js")
script(src="/socket.io/socket.io.js")
!= js('todo')
```

你可能想知道这个文件是从哪里来的。接下来，我们将设置套接字。IO 中间件在我们的`app.coffee`文件中。这将为我们托管客户端库。

# 服务器上的 WebSocket

我们已经准备好客户端代码来发出网络套接字请求；现在我们可以继续到我们的节点后端。首先，我们需要设置我们的套接字。IO 中间件。对此有一个小小的警告，我们不能使用套接字。IO 直接作为中间件的 Express 应用，自 Socket。IO 需要 Node.js HTTP 服务器，不直接支持 Express。相反，我们将使用内置的 Node.js HTTP 模块创建一个 web 服务器，将我们的 Express 应用作为`requestListener` 传递。然后我们可以使用 Socket 中的`listen`功能。连接到服务器的输入输出。

以下是代码在我们的`app.coffee`文件中的外观:

```js
express = require 'express'
path = require 'path'

app = express()
server = (require 'http').createServer app
io = (require 'socket.io').listen server

app.set 'views', path.join __dirname, 'views'
app.set 'view engine', 'jade'
app.use(express.static(path.join __dirname, 'public'))
app.use (require 'connect-assets')()

app.get '/', (req, res) ->
  res.render 'index'

io.sockets.on 'connection', (socket) =>
 console.log('connected')
 socket.on 'joinList', (list) => console.log "Joining list #{list}"

server.listen(3000)
console.log('Listening on port 3000')
```

`io.sockets.on 'connection'`功能处理客户端连接时的事件。在这里，我们登录到我们连接的控制台，然后设置`joinList`消息处理程序。现在，我们将把从客户端收到的值记录到控制台。

我们现在应该能够测试连接到列表。刷新我们的待办事项列表主页，并输入要加入的列表名称。点击**加入**按钮后，前往我们的后台主管任务。您应该会看到类似以下消息的内容:

**已连接**

**加入名单迈克尔的名单**

成功了。我们已经成功创建了一个双向网络套接字连接。到目前为止，我们还没有真正加入一个列表，所以让我们现在就开始吧。

# 加入列表

要加入列表，我们将使用 Socket 的一个特性。木卫一称**房间**为。它允许套接字。IO 服务器对其客户端进行分段，并向所有连接的客户端的子集发送消息。在服务器上，我们将跟踪每个房间的待办事项列表，然后告诉客户端在连接时同步其本地列表。

我们将用下面代码片段中显示的高亮代码更新`app.coffee`文件:

```js
@todos = {}
io.sockets.on 'connection', (socket) =>
  console.log('connected')
  socket.on 'joinList', (list) =>
    console.log "Joining list #{list}"
 socket.list = list
 socket.join(list)
 @todos[list] ?= []
 socket.emit 'syncItems', @todos[list]

```

我们将`@todos`实例变量初始化为一个空散列。它将保存每个房间的待办事项列表，使用列表名称作为关键字。在`joinList`处理函数中，我们将`socket`变量的`list`属性设置为等于客户端传入的列表名称。

然后我们使用`socket.join`功能将我们的列表加入到一个同名的房间。如果房间还不存在，它将被创建。然后我们给`@todos`中的项目分配一个空数组值，键等于`list`。如果对象是`null`，则`?=`操作员只会将右侧的值分配给左侧的对象。

最后，我们使用`socket.emit`功能向客户端发送消息。`syncItems`标识符将告诉它将其本地数据与我们传递给它的待办事项同步。

为了处理`syncItems`消息，我们需要用以下突出显示的代码更新`todo.coffee`文件:

```js
  joinList: ->
    @socket = io.connect('http://localhost:3000')
    @socket.on 'connect', => 
   @socket.emit 'joinList', @$joinListName.val()

 @socket.on 'syncItems', (items) =>
 @syncItems(items)

 syncItems: (items) ->
 console.log 'syncing items'
 localStorage.clear()
 localStorage.setObj item.id, item for item in items
 @displayItems()

```

加入列表后，我们建立客户端连接来处理`syncItems`消息。我们希望收到我们刚刚加入的列表中的所有待办事项。`syncItems`功能将清除`localStorage`中的所有当前项目，添加所有新项目，然后显示它们。

## 用户界面

最后，让我们更新我们的用户界面，这样用户就可以知道他们什么时候加入了一个列表，并让他们离开它。我们将在`index.jade`文件中修改我们的`#footer div`标签，如下所示:

```js
doctype 5
html
  head
  title  Collaborative Todo
  link(rel="stylesheet", href="css/todo.css")
  body
    section#todoapp
      header#header
        h1 todos
        input#new-todo(placeholder="What needs to be done?", autofocus=true)
      section#main
        ul#todo-list
 footer#footer
 section#connect
          | Join list:
          input#join-list-name
          button#join Join
          button#clear-completed Clear completed
 section#disconnect.hidden
 | Joined list: &nbsp
 span#connected-list List name
 button#leave Leave
    script(src="//ajax.googleapis.com/ajax/libs/jquery/1.7.2/jquery.min.js")
    script(src="/socket.io/socket.io.js")
    != js('todo')
```

在之前的标记中，我们在`footer div`标签中添加了两个新的部分。根据我们处于列表中的哪个状态`connected`或`disconnected`，每个部分将被隐藏或可见。`connect`部分与之前相同。`disconnect`部分将显示您当前连接的列表，并有**离开**按钮。

现在我们将在`todo.coffee`文件中添加代码，以便在加入列表时更新用户界面。

首先，我们将在我们的`cacheElements`函数中缓存新元素，如下面的代码片段所示:

```js
cacheElements: ->
    @$input = $('#new-todo')
    @$todoList = $('#todo-list')
    @$clearCompleted = $('#clear-completed')
 @$joinListName = $("#join-list-name")
 @$join = $('#join')
 @$connect = $('#connect')
 @$disconnect = $('#disconnect')
 @$connectedList = $('#connected-list')
 @$leave = $('#leave')

```

接下来，我们将更改用户界面，以显示当调用`syncItems`时，我们处于`connected`状态(成功加入列表后，服务器会触发该状态)。我们使用`@currentList`功能，我们将在`joinList`功能中进行设置；添加以下代码片段中突出显示的代码:

```js
  joinList: ->
    @socket = io.connect('http://localhost:3000')
    @socket.on 'connect', =>
 @currentList = @$joinListName.val()
      @socket.emit 'joinList', @currentList

    @socket.on 'syncItems', (items) => @syncItems(items)

  syncItems: (items) ->
    console.log 'syncing items'
    localStorage.clear()
    localStorage.setObj item.id, item for item in items
    @displayItems()
 @displayConnected(@currentList)

 displayConnected: (listName) ->
 @$disconnect.removeClass 'hidden'
 @$connectedList.text listName
 @$connect.addClass 'hidden'

```

`displayConnected`功能只是隐藏`connect`部分，显示`disconnect`部分。

## 留下名单

留下清单应该很容易。我们断开当前的套接字连接，然后更新用户界面。

为了处理点击按钮时的`disconnect`动作，我们在`bindEvents`函数中添加了一个处理程序，如下面的代码片段所示:

```js
bindEvents: ->
    @$input.on 'keyup', (e) => @create e
    @$todoList.on 'click', '.destroy', (e) => @destroy e.target
    @$todoList.on  'change', '.toggle', (e) => @toggle e.target
    @$clearCompleted.on 'click', (e) => @clearCompleted()
    @$join.on 'click', (e) => @joinList()
 @$leave.on 'click', (e) => @leaveList()

```

如您所见，我们添加的处理程序将只调用一个`leaveList`函数。我们仍然需要实施它。在我们的`TodoApp`类中定义的最后一个函数之后，在类的末尾添加以下两个函数:

```js
 leaveList: ->
    @socket.disconnect() if @socket
    @displayDisconnected()

  displayDisconnected: () ->
    @$disconnect.addClass 'hidden'
    @$connect.removeClass 'hidden'
```

## 测试这一切

现在让我们测试我们的列表加入和离开代码。要查看所有操作，请遵循以下步骤:

1.  在浏览器中打开`http://localhost:3000/`。
2.  在浏览器窗口中，输入列表名称，点击**加入列表**。用户界面应该按照预期更新。
3.  加入列表后，添加一些待办事项。
4.  现在再次打开网站，这次使用第二个浏览器。由于`localStorage`是浏览器独有的，我们这样做是为了有一个干净的待办事项列表。
5.  再次输入与在其他浏览器中相同的列表名称，点击**加入列表**。
6.  随着列表的同步，您现在应该会在显示之前看到您添加的列表项目。
7.  最后，使用**离开**按钮从列表中断开。

![Testing it all](img/9588_05_02.jpg)

从不同浏览器同步的两个列表

太棒了！我们现在可以看到网络套接字的力量在发挥作用。我们的客户端被通知何时应该同步项目，而不必轮询服务器。

然而，一旦我们连接到列表，我们仍然不能添加新的项目来让它们出现在房间中的所有其他客户中。让我们实现它。

# 将待办事项添加到共享列表

首先，我们将处理在服务器上添加新项目。处理这个问题的最好方法是在现有的`create`功能中创建待办事项。不仅仅是将它们添加到`localStorage`中，我们还会向服务器发出一条消息，告诉它已经创建了一个新的待办事项，并将其作为参数传递。将`create`功能修改为如下代码:

```js
create: (e) ->
    val = $.trim @$input.val()
    return unless e.which == 13 and val

    randomId = Math.floor Math.random()*999999

 newItem =
 id: randomId
 title: val
 completed: false

 localStorage.setObj randomId, newItem
 @socket.emit 'newItem', newItem if @socket
    @$input.val ''
    @displayItems()
```

我们需要在服务器上处理`newItem`消息。我们将在`app.coffee`中设置当客户端加入列表时这样做的代码。

让我们修改之前添加的`joinList`事件处理程序；在下面的代码片段中添加突出显示的代码:

```js
io.sockets.on 'connection', (socket) =>
  console.log("connected")
  socket.on 'joinList', (list) =>
    console.log "Joining list #{list}"
    socket.list = list
    socket.join(list)
    @todos[list] ?= []

    socket.emit 'syncItems', @todos[list]

 socket.on 'newItem', (todo) =>
 console.log "new todo #{todo.title}"
 @todos[list].push todo
 io.sockets.in(socket.list).emit('itemAdded', todo)

```

在这个代码片段中，当用户加入列表时，我们设置了另一个`socket`事件。在这种情况下，是为了`newItem`事件。我们使用`push`功能将新的待办事项添加到我们的`@todos`列表中。然后，我们向当前列表中的所有客户端发出新的`itemAdded`消息。

这个`itemAdded`消息会怎么样？你猜对了；它将在客户端再次得到处理。这种来回消息传递在网络套接字应用中非常常见，需要一些时间来适应。不过，不要担心；一旦你掌握了窍门，事情就会变得简单。

同时让我们在客户端处理`itemAdded`事件。我们还通过在下面的代码片段中添加高亮显示的代码，在我们的`joinList`方法中设置了这段代码:

```js
joinList: ->
    @socket = io.connect('http://localhost:3000')
    @socket.on 'connect', =>
      @currentList = @$joinListName.val()
      @socket.emit 'joinList', @currentList

    @socket.on 'syncItems', (items) => @syncItems(items)

 @socket.on 'itemAdded', (item) =>
 localStorage.setObj item.id, item
 @displayItems()

```

我们通过用项目标识和值调用`localStorage.setObject`来处理`itemAdded`事件。这将创建一个新的待办事项，如果它不在`localStorage`中，或者它将更新现有的值。

就这样！我们现在应该能够向列表中的所有客户端添加项目。为了测试它，我们将遵循与之前类似的步骤:

1.  在浏览器中打开`http://localhost:3000/`。
2.  在浏览器窗口中，输入列表名称，点击**加入列表**。用户界面应该按照预期更新。
3.  现在再次打开网站，这次使用第二个浏览器。
4.  再次输入与在其他浏览器中相同的列表名称，点击**加入列表**。
5.  在任一浏览器中添加新的待办事项。您将立即在另一个浏览器中看到待办事项。

哇哦！这不是令人印象深刻吗？

# 从共享列表中删除待办事项

要从共享列表中删除待办事项，我们将按照类似的模式添加项目。在`todo.coffee`中的`destroy`函数中，我们将向我们的套接字发出`removeItem`消息，让服务器知道应该删除一个项目，如以下代码片段所示:

```js
destroy: (elem) ->
    id = ($(elem).closest 'li').data('id')
    localStorage.removeItem id
 @socket.emit 'removeItem', id if @socket
    @displayItems()
```

我们再次设置服务器端代码，通过从内存中的共享列表中移除该项目来处理该消息，然后通知连接到该列表的所有客户端该项目已被移除:

```js
io.sockets.on 'connection', (socket) =>
  console.log("connected")
  socket.on 'joinList', (list) =>
    console.log "Joining list #{list}"
    socket.list = list
    socket.join(list)
    @todos[list] ?= []

    socket.emit 'syncItems', @todos[list]

    socket.on 'newItem', (todo) =>
      console.log "new todo #{todo.title}"
      @todos[list].push todo
      io.sockets.in(socket.list).emit('itemAdded', todo)

 socket.on 'removeItem', (id) =>
 @todos[list] = @todos[list].filter (item) -> item.id isnt id
 io.sockets.in(socket.list).emit('itemRemoved', id)

```

`removeItem`套接字事件处理程序获取待办事项的标识，以删除传递给它的任务。它通过将共享列表的当前值分配给我们使用 JavaScript 的数组`filter`函数创建的新值，从列表中删除待办事项。这将选择所有没有传递标识的项目。然后它用`itemRemoved`消息调用共享列表中所有客户端套接字连接上的`emit`。

最后，我们需要在客户端处理`itemRemoved`消息。与添加项目时类似，我们将在`todo.coffee`中的`joinList`函数中进行设置，如以下代码片段所示:

```js
joinList: ->
    @socket = io.connect('http://localhost:3000')
    @socket.on 'connect', =>
      @currentList = @$joinListName.val()
      @socket.emit 'joinList', @currentList

    @socket.on 'syncItems', (items) => @syncItems(items)

    @socket.on 'itemAdded', (item) =>
      localStorage.setObj item.id, item
      @displayItems()

 @socket.on 'itemRemoved', (id) =>
 localStorage.removeItem id
 @displayItems()

```

我们从`localStorage`中移除该项目并更新 UI。

要测试移除项目，请执行以下步骤:

1.  在浏览器中打开`http://localhost:3000/`。
2.  在浏览器窗口中，输入列表名称，点击**加入列表**。用户界面应该按照预期更新。
3.  连接到共享列表后，添加一些待办事项。
4.  现在再次打开网站，这次使用第二个浏览器。
5.  再次输入与在其他浏览器中相同的列表名称，点击**加入列表**。您的待办事项列表将与共享列表同步，并将包含您在其他浏览器中添加的项目。
6.  单击删除图标，在任一浏览器中删除待办事项。您将看到删除的待办事项立即在另一个浏览器中消失。

## 现在，轮到你了

作为对你的最后一个练习，我将要求你让**清除完成**按钮工作。作为提示，您应该能够使用现有的`destroyItem`方法功能。

# 总结

在这一章中，我们通过探索 Node.js 作为一个快速的、事件驱动的平台来完成了对咖啡脚本生态系统的考察，该平台允许您使用 JavaScript 或咖啡脚本来编写服务器应用。我希望您已经看到了能够在服务器和浏览器上同时使用 CoffeeScript 编写 web 应用的乐趣。

我们还花了一些时间研究一些为 Node.js 编写的优秀开源库和框架，比如 expressjs、connect 和 Socket。IO，并且已经看到了我们如何成功地使用 npm 来管理我们应用中的依赖关系和模块。

我们的示例应用正是您将使用 Node.js 的那种东西，我们看到了它的事件驱动模型如何适合于编写客户端和服务器之间有大量持续交互的应用。

既然我们的旅程已经结束，我希望已经向你灌输了走出去并使用咖啡脚本来改变世界的渴望和技能。我们不仅花了一些时间探索语言，还探索了优秀的工具、库和框架，这些工具、库和框架使我们能够用更少的代码更快地开发强大的应用。

咖啡脚本和 JavaScript 生态系统的未来是光明的，希望你能成为其中的一员！